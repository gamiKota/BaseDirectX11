// FBX用頂点シェーダ (FbxModelVertex.hlsl)

#define MAX_BONE_MATRIX	256

// カメラ
cbuffer global_camera : register(b0) {
	float4	g_cameraPos;	// 視点座標(ワールド空間)
	matrix	g_View;			// ビュー
	matrix	g_Proj;			// プロジェクション
};

// ライト
cbuffer global_light : register(b1) {
	float4	g_lightDir;			// 光源方向(ワールド空間)
	float4	g_lightAmbient;		// 環境光
	float4	g_lightDiffuse;		// 拡散光
	float4	g_lightSpecular;	// 鏡面反射光
};

// 描画オブジェクトの行列情報
cbuffer global_world : register(b3) {
	matrix	g_World;		// ワールド行列
	matrix	g_mTexture;		// テクスチャ行列
};

// ボーンのポーズ行列
cbuffer global_bones : register(b5) {
	matrix g_BoneWorld[MAX_BONE_MATRIX];
};

// パラメータ
struct VS_INPUT {
	float3	Pos		: POSITION;
	float3	Normal	: NORMAL;
	float2	Tex		: TEXCOORD0;
	uint4	Bone	: BONE_INDEX;	// ボーンのインデックス
	float4	Weight	: BONE_WEIGHT;	// ボーンの重み
};

struct VS_OUTPUT {
	float4	Pos			: SV_Position;
	float2	Tex			: TEXCOORD0;
	float3	Normal		: TEXCOORD1;
	float3	PosForPS	: TEXCOORD2;	// wPos
};

// スキニング後の頂点・法線
struct SKIN {
	float4 Pos;
	float3 Norm;
};

// 頂点をスキニング (ボーンにより移動)
SKIN SkinVert(VS_INPUT input)
{
	SKIN output;
	float4 Pos = float4(input.Pos, 1.0f);
	float3 Norm = input.Normal;
	if (input.Weight.x == 0.0f) {
		// ボーン無し
		output.Pos = Pos;
		output.Norm = Norm;
	}
	else {
		// ボーン0
		uint uBone = input.Bone.x;
		float fWeight = input.Weight.x;
		matrix m = g_BoneWorld[uBone];
		output.Pos = fWeight * mul(Pos, m);
		output.Norm = fWeight * mul(Norm, (float3x3)m);
		// ボーン1
		uBone = input.Bone.y;
		fWeight = input.Weight.y;
		m = g_BoneWorld[uBone];
		output.Pos += fWeight * mul(Pos, m);
		output.Norm += fWeight * mul(Norm, (float3x3)m);
		// ボーン2
		uBone = input.Bone.z;
		fWeight = input.Weight.z;
		m = g_BoneWorld[uBone];
		output.Pos += fWeight * mul(Pos, m);
		output.Norm += fWeight * mul(Norm, (float3x3)m);
		// ボーン3
		uBone = input.Bone.w;
		fWeight = input.Weight.w;
		m = g_BoneWorld[uBone];
		output.Pos += fWeight * mul(Pos, m);
		output.Norm += fWeight * mul(Norm, (float3x3)m);
	}
	return output;
}

//
// 頂点シェーダ
//
VS_OUTPUT main(VS_INPUT input)
{
	matrix WVP;
	VS_OUTPUT output;
	SKIN vSkinned	= SkinVert(input);
	WVP				= mul(g_World, g_View);
	WVP				= mul(WVP, g_Proj);
	output.Pos		= mul(vSkinned.Pos, WVP);
	output.Tex		= input.Tex;
	output.Normal	= mul(vSkinned.Norm, (float3x3)g_World);
	output.PosForPS = mul(vSkinned.Pos, g_World).xyz;
	return output;
}
